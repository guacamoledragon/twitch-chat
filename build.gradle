buildscript {
    ext.kotlin_version = '1.3.72'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "org.jetbrains.intellij" version "0.4.21"
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
}

dependencies {
    compile 'io.socket:socket.io-client:0.7.0'
    testCompile 'org.jetbrains.spek:spek:0.1.195'
}

intellij {
    pluginName = name
}

scmVersion {
    versionIncrementer 'incrementMinor'
    tag {
        prefix = ''
    }

    String pluginPath = "src/main/resources/META-INF/plugin.xml"

    hooks {
        pre 'fileUpdate', [ file: pluginPath
                          , pattern: {v, c -> /(<version>)${c.readVersion()}/}
                          , replacement: {v, c -> "\$1$v"}
                          ]

        pre { c -> c.addCommitPattern(pluginPath) }

        pre 'commit'
    }
}

task deleteLockFile(type: Delete) {
    delete ".gradle/$gradle.gradleVersion/taskArtifacts/cache.properties.lock"
}

group 'club.guacamoledragon.plugin'
version scmVersion.version